apply plugin: 'groovy'

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        //See: https://github.com/asciidoctor/asciidoctor-gradle-plugin
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.2'
    }
}

repositories {
    jcenter()
}

configurations {
    ivy
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.4'
    ivy "org.apache.ivy:ivy:2.4.0"
}

tasks.withType(GroovyCompile) {
    groovyClasspath += configurations.ivy
}

apply plugin: 'codenarc'
codenarc.toolVersion = 0.24
codenarcMain {
    ignoreFailures false
    maxPriority1Violations 0
    maxPriority2Violations 5
    maxPriority3Violations 10
    configFile file('config/codenarc/ruleset.groovy')
}

apply plugin: 'org.asciidoctor.convert'
asciidoctor {
    resources {
        from('src/main/groovy') {
            include '*.groovy'
            include 'resources/*'
        }
        into 'src'
    }
}

task publish(type: GradleBuild, dependsOn: [asciidoctor, check]) {
    buildFile = 'publish.gradle'
    tasks = ['publishGhPages']
}
